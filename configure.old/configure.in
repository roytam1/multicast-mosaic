dnl Process this file with autoconf to produce a configure script.
dnl generated by autoscan
dnl mjr: 9.2.1998

AC_INIT()

MCVER=3.4.0
AC_SUBST(MCVER)

dnl ################# some macro definitions ###################
dnl 	to detect details on X and Motif
dnl     partly taken from lesstif
dnl ############################################################

AC_DEFUN(AC_PATH_MOTIF_DIRECT,
[test -z "$motif_direct_test_library" && motif_direct_test_library=Xm
test -z "$motif_direct_test_function" && motif_direct_test_function=XmCreatePushButton
test -z "$motif_direct_test_include" && motif_direct_test_include=Xm/Xm.h
  for ac_dir in               \
    /usr/include/Motif1.2     \
    /usr/Motif1.2/include     \
                              \
    /usr/motif/include        \
                              \
    /usr/X11R6/include        \
    /usr/X11R5/include        \
                              \
    /usr/include/X11R6        \
    /usr/include/X11R5        \
                              \
    /usr/local/X11R6/include  \
    /usr/local/X11R5/include  \
                              \
    /usr/local/include/X11R6  \
    /usr/local/include/X11R5  \
                              \
    /usr/X11/include          \
    /usr/include/X11          \
    /usr/local/X11/include    \
    /usr/local/include/X11    \
                              \
    /usr/X386/include         \
    /usr/x386/include         \
    /usr/XFree86/include/X11  \
                              \
    /usr/dt/include           \
                              \
    /usr/include              \
    /usr/local/include        \
    /usr/unsupported/include  \
    /usr/athena/include       \
    /usr/local/x11r5/include  \
    /usr/lpp/Xamples/include  \
    ; \
  do
    if test -r "$ac_dir/$motif_direct_test_include"; then
      no_motif= ac_motif_includes=$ac_dir
      break
    fi
  done

# Check for the libraries.
# See if we find them without any special options.
# Don't add to $LIBS permanently.
ac_save_LIBS="$LIBS"
LIBS="-l$motif_direct_test_library $LIBS"
# First see if replacing the include by lib works.
for ac_dir in `echo "$ac_motif_includes" | sed s/include/lib/` \
    /usr/lib/Motif1.2     \
    /usr/Motif1.2/lib     \
                          \
    /usr/motif/lib        \
                          \
    /usr/X11R6/lib        \
    /usr/X11R5/lib        \
                          \
    /usr/lib/X11R6        \
    /usr/lib/X11R5        \
                          \
    /usr/local/X11R6/lib  \
    /usr/local/X11R5/lib  \
                          \
    /usr/local/lib/X11R6  \
    /usr/local/lib/X11R5  \
                          \
    /usr/X11/lib          \
    /usr/lib/X11          \
    /usr/local/X11/lib    \
                          \
    /usr/X386/lib         \
    /usr/x386/lib         \
    /usr/XFree86/lib/X11  \
                          \
    /usr/lib              \
    /usr/local/lib        \
    /usr/unsupported/lib  \
    /usr/athena/lib       \
    /usr/local/x11r5/lib  \
    /usr/lpp/Xamples/lib  \
    ; \
do
  for ac_extension in a so sl; do
    if test -r $ac_dir/lib${motif_direct_test_library}.$ac_extension; then
      no_motif= ac_motif_libraries=$ac_dir
      break 2
    fi
  done
done
LIBS=$ac_save_LIBS
ac_sav_LIBS=""
])

AC_DEFUN(AC_PATH_MOTIF,
[AC_REQUIRE_CPP()dnl

motif_includes=NONE
motif_libraries=NONE

AC_MSG_CHECKING(for Motif)
AC_ARG_WITH(motif, [  --with-motif            enable Motif tests])
if test "x$with_motif" = xno; then
  no_motif=yes
else
  if test "x$motif_includes" != xNONE && test "x$motif_libraries" != xNONE; then
    no_motif=
  else
AC_CACHE_VAL(ac_cv_path_motif,
[# One or both of these vars are not set, and there is no cached value.
no_motif=yes
AC_PATH_MOTIF_DIRECT

if test "$no_motif" = yes; then
  ac_cv_path_motif="no_motif=yes"
else
  ac_cv_path_motif="no_motif= ac_motif_includes=$ac_motif_includes ac_motif_libraries=$ac_motif_libraries"
fi])dnl
  fi
  eval "$ac_cv_path_motif"
fi # with_motif != no

if test "$no_motif" = yes; then
dnl mjr--  AC_MSG_RESULT(no)
  AC_MSG_ERROR([... no Motif compatible libraries found. Please check Your installation])
else
  test "x$motif_includes" = xNONE && motif_includes=$ac_motif_includes
  test "x$motif_libraries" = xNONE && motif_libraries=$ac_motif_libraries
  ac_cv_path_motif="no_motif= ac_motif_includes=$motif_includes ac_motif_libraries=$motif_libraries"
  AC_MSG_RESULT([libraries $motif_libraries, headers $motif_includes])
fi
])

AC_DEFUN(AC_XT_VERSION6,
[AC_MSG_CHECKING([For Xt Revision Number 6])
  ac_save_CFLAGS=$CFLAGS
  CFLAGS=`echo $CFLAGS -I$x_includes`
  AC_TRY_LINK([#include <${x_includes}/X11/Intrinsic.h>],
[#if XtSpecificationRelease < 6
fail;
#endif
], XTversion="6",
   [AC_MSG_RESULT(no)])
  CFLAGS=$ac_save_CFLAGS
])

AC_DEFUN(AC_XT_VERSION5,
[AC_MSG_CHECKING([For Xt Revision Number 5])
  ac_save_CFLAGS=$CFLAGS
  CFLAGS=`echo $CFLAGS -I$x_includes`
  AC_TRY_LINK([#include <${x_includes}/X11/Intrinsic.h>],
[#if XtSpecificationRelease < 5
fail;
#endif
], XTversion="5",
   [AC_MSG_RESULT(no)])
  CFLAGS=$ac_save_CFLAGS
])

dnl crude check to determine old Motif 1.2 system.
AC_DEFUN(AC_MOTIF_1_2,
[AC_MSG_CHECKING([For Motif 1.2x])
  ac_save_CFLAGS=$CFLAGS
  CFLAGS=`echo $CFLAGS -I$x_includes -I$motif_includes`
  AC_TRY_LINK([#include <${motif_includes}/Xm/Xm.h>],
[#if XmVersion > 1999
fail;
#endif
], MOTIF_VERSION="-DMOTIF1_2",
   [AC_MSG_RESULT(no)])
  CFLAGS=$ac_save_CFLAGS
  AC_MSG_RESULT([Motif-Version: $MOTIF_VERSION])
  AC_SUBST(MOTIF_VERSION)
])

dnl ##############################################################
dnl                 now really run the tests 
dnl ##############################################################

dnl Checks for programs.
AC_PROG_CC
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB

dnl mjr: check system type
dnl
dnl mjr++ evaluate result of AC_CANONICAL_SYSTEM
AC_CANONICAL_SYSTEM
case "$target" in
  *linux*)
    OSDEF="-Dlinux"
    AC_SUBST(OSDEF)
    MO_MACHINE_TYPE=`uname -rs`
    AC_SUBST(MO_MACHINE_TYPE)
  ;;
  *solaris2*)
    OSDEF="-DSOLARIS -DSVR4"
    AC_SUBST(OSDEF)
    MO_MACHINE_TYPE=`uname -rs`
    AC_SUBST(MO_MACHINE_TYPE)
  ;;
dnl mjr the following cases are untested!
  hppa*-*-*)
    if test `echo $CC | awk '{ print $1 }'` = "gcc" ; then
	OSDEF="-D_HPUX_SOURCE"
    else
	OSDEF="-Aa -D_HPUX_SOURCE"
    fi
    AC_SUBST(OSDEF)
    MO_MACHINE_TYPE=`uname -rs`
    AC_SUBST(MO_MACHINE_TYPE)
  ;;
  *sunos*)
    OSDEF="-DSUNOS"
    AC_SUBST(OSDEF)
    MO_MACHINE_TYPE=`uname -rs`
    AC_SUBST(MO_MACHINE_TYPE)
  ;;
  *aix*)
    OSDEF="-D_BSD"
    AC_SUBST(OSDEF)
    MO_MACHINE_TYPE=`uname -rs`
    AC_SUBST(MO_MACHINE_TYPE)
  ;;
  *)
    OSDEF="Unknown Platform"
    AC_SUBST(OSDEF)
    MO_MACHINE_TYPE=`uname -rs`
    AC_SUBST(MO_MACHINE_TYPE)
  ;;
esac


AC_CHECK_LIB(socket, main)

dnl Checks for X and Motif
AC_PATH_X
AC_PATH_XTRA

AC_XT_VERSION6

if test "x$XTversion" = x ; then
  AC_XT_VERSION5
  if test "x$XTversion" = x; then
    AC_MSG_ERROR( You must have X11 Revision 5 or higher! )
  else
    AC_MSG_RESULT(revision 5)
   R6libs="-lXext -lX11"
  fi

else
  AC_MSG_RESULT(revision 6)
  R6libs="-lXext -lSM -lICE"
fi
AC_PATH_MOTIF

if test "$no_motif" != "yes"; then
 X_CFLAGS=`echo $X_CFLAGS -I$motif_includes`
 X_PRE_LIBS=`echo  -L$motif_libraries  $X_PRE_LIBS -lXm -lXmu -lXt  $R6libs`
fi
AC_MOTIF_1_2

AC_SUBST(X_CFLAGS)
AC_SUBST(X_LIBS)
AC_SUBST(X_PRE_LIBS)
AC_SUBST(X_EXTRA_LIBS)

dnl AC_HEADER_DIRENT
dnl AC_HEADER_STDC
dnl AC_HEADER_SYS_WAIT
dnl AC_CHECK_HEADERS(fcntl.h malloc.h sys/ioctl.h sys/time.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
dnl AC_TYPE_UID_T
dnl AC_TYPE_PID_T
dnl AC_TYPE_SIZE_T
dnl AC_HEADER_TIME
dnl AC_STRUCT_TM

dnl check for png and for jpeg
AC_CHECK_LIB(z, gzopen, zflags="true")
if [[ "$zflags" = "true" ]]; then 
 AC_CHECK_LIB(png, png_warning, pnglibs=-lpng pngflags=-DHAVE_PNG,  pngflags=-UHAVE_PNG, -lz -lm)
 if [[ -n "$pnglibs" ]] ; then
  pnglibs=`echo $pnglibs " -lz -lm"`

 fi
fi
AC_SUBST(pnglibs)
AC_SUBST(pngflags)

AC_CHECK_LIB(jpeg, jpeg_std_error, jpegflags=-DHAVE_JPEG jpeglibs=-ljpeg)
AC_SUBST(jpeglibs)
AC_SUBST(jpegflags)
  
dnl Checks for library functions.
dnl AC_PROG_GCC_TRADITIONAL
AC_TYPE_SIGNAL
dnl AC_FUNC_STRFTIME
dnl AC_FUNC_VFORK
dnl AC_FUNC_VPRINTF
dnl AC_FUNC_WAIT3
dnl AC_CHECK_FUNCS(getcwd gethostname gettimeofday mkdir mktime select socket strdup strstr strtol uname)

AC_OUTPUT(Makefile src/mosaic.h)
dnl AC_OUTPUT(Makefile)

